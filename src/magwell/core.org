#+title: Core
This is the primary endpoint to generate the g36 compatible magwell for the bren 805.

* Namespace
This loads all the namespaces we'll need for the project.
scad-clj is the only external library we're using.

#+begin_src clojure :tangle core.clj
(ns magwell.core
  (:require
   [scad-clj.model :as m]
   [magwell.io :as mio]
   [magwell.shell :as shell]
   [magwell.mag-inner :as mag-inner]
   [magwell.mag-rim :as mag-rim]
   [magwell.mag-ear :as mag-ear]
   [magwell.pinhole :as pinhole]
   [magwell.ear-shelf :as ear-shelf]
   [magwell.dovetail :as dovetail]
   [magwell.ear-ridge :as ear-ridge]
   [magwell.x-bore :as x-bore]
   [magwell.params :as p]
   [magwell.lever-pocket :as lever-pocket]))
#+end_src

* Model
This creates the model to be saved as a magwell.scad file in -main. The following operations happen in order

** 1.Base subtraction

*** Subtracts all internal cavities and interface cutouts that must exist inside the shell.

[[file:shell.org][shell-with-slant-cut]] -  creates the bounding box and initial geometry of the magwell.

[[file:mag-inner.org][mag-inner-cutter]] – clears the internal volume for magazine

[[file:lever_pocket.org][lever-pocket]] – creates space for the release/retention mechanism.

dovetail – subtracts the mating interface geometry.

mag-rim / mag-rim-mirrored / mag-rim-z – forms rim reliefs and alignment features.

** 2.External additions
Unions on the magazine ear geometry and reinforcing ridges.

** 3.Final subtractions
Cuts away shelves, bores, and pinholes required for assembly, alignment, and actuation.

** 4.Code
#+begin_src clojure :tangle core.clj
(defn model
  []
  (m/difference
   (m/union
    (m/difference
     ;; First differences
     (shell/shell-with-slant-cut)
     (mag-inner/mag-inner-cutter)
     (lever-pocket/lever-pocket)
     (dovetail/dovetail)
     (mag-rim/mag-rim)
     (mag-rim/mag-rim-mirrored)
     (mag-rim/mag-rim-z))

    ;; unions
    (mag-ear/mag-ear)
    (ear-ridge/ear-ridges))

   ;; Last differences.
   (ear-shelf/ear-shelf)
   (mag-ear/mag-ear-hole)
   (x-bore/x-bore)
   (pinhole/pinhole)
   (pinhole/pinhole-2)))
#+end_src

* Debug-model
This creates the debug model, which can be useful if you're trying to branch two different states.
It's meant to be rebuilt depending on the scenario. right now it's just doing a union of the ear ridges on the main model.
#+begin_src clojure :tangle core.clj
(defn debug-model []
  (m/union
   (model)
   (ear-ridge/ear-ridges-visual)))
#+end_src

* -main
This is used to spit out the "magwell.scad" file. It also loads up external stl files.
Currently the sidecutters are the only ones that are active.
#+begin_src clojure :tangle core.clj
(defn -main
  [& _]
  (binding [scad-clj.model/*center* false]
    (mio/write-scad-with-import-cutters!
     "out/magwell.scad"
     (model)
     [{:pos (:pos p/sidecutter)   :rot (:rot p/sidecutter)   :stl "sidecutter.stl"}
      {:pos (:pos p/sidecutter-2) :rot (:rot p/sidecutter-2) :stl "sidecutter.stl"}])))
#+end_src
