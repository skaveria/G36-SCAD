#+title: Core
This is the primary endpoint to generate the g36 compatible magwell for the bren 805.

* Namespace
This loads all the namespaces we'll need for the project.
scad-clj is the only external library we're using.

#+begin_src clojure :tangle core.clj
(ns magwell.core
  (:require
   [scad-clj.model :as m]
   [magwell.io :as mio]
   [magwell.shell :as shell]
   [magwell.mag-inner :as mag-inner]
   [magwell.mag-rim :as mag-rim]
   [magwell.mag-ear :as mag-ear]
   [magwell.pinhole :as pinhole]
   [magwell.ear-shelf :as ear-shelf]
   [magwell.dovetail :as dovetail]
   [magwell.ear-ridge :as ear-ridge]
   [magwell.x-bore :as x-bore]
   [magwell.params :as p]
   [magwell.lever-pocket :as lever-pocket]))
#+end_src

* Model
This creates the model to be saved as a `magwell.scad` file in -main. The following operations happen in order

** 1. Base subtraction

*** Subtracts all internal cavities and interface cutouts that must exist inside the shell.

[[file:shell.org][shell-with-slant-cut]] -  creates the bounding box and initial geometry of the magwell.

[[file:mag-inner.org][mag-inner-cutter]] – clears the internal volume for magazine

[[file:lever_pocket.org][lever-pocket]] – creates space for the release/retention mechanism.

[[file:dovetail.org][dovetail]] – subtracts the mating interface geometry.

[[file:mag_rim.org][mag-rim-final]] – forms rim reliefs and alignment features.

** 2. External additions

*** Add all extranal parts to the model

[[file:mag_ear.org][mag-ear]] - This is the circular tab that has the will hold the pivot hole

[[file:ear_ridges.org][ear-ridges]] - These are just some ridgs at the front of the magwell so it doesn't look so weird

** 3. Final subtractions

*** Final subtractions on compound objects

[[file:ear_shelf.org][ear-shelf]] - This is the cutout inside of the ear that allows for the lower to index on the upper.

[[file:mag_ear.org][mag-ear-hole]] - This is the hole inside of the ear that holds the pin that keeps the upper and lower receivers together.

[[file:x_bore.org][x-bore]] - This is a hole that's cut out of the top of the magwell to allow clearance for the barrel.

[[file:pinhole.org][pinhole]] - This is the primary pinhole that connects the magwell to the rest of the lower.

[[file:pinhole.org][pinhole-2]] - This is an additional pinhole to act as a pivot for the mag release lever.

** 4. Code
#+begin_src clojure :tangle core.clj
(defn model
  []
  (m/difference
   (m/union
    (m/difference
     ;; First differences
     (shell/shell-with-slant-cut)
     (mag-inner/mag-inner-cutter)
     (lever-pocket/lever-pocket)
     (dovetail/dovetail)
     (mag-rim/mag-rim-final))

    ;; unions
    (mag-ear/mag-ear)
    (ear-ridge/ear-ridges))

   ;; Last differences.
   (ear-shelf/ear-shelf)
   (mag-ear/mag-ear-hole)
   (x-bore/x-bore)
   (pinhole/pinhole)
   (pinhole/pinhole-2)))
#+end_src

* Debug-model
This creates the debug model, which can be useful if you're trying to branch two different states.
It's meant to be rebuilt depending on the scenario. right now it's just doing a union of the ear ridges on the main model.
#+begin_src clojure :tangle core.clj
(defn debug-model []
  (m/union
   (model)
   (ear-ridge/ear-ridges-visual)))
#+end_src

* -main
This is used to spit out the "magwell.scad" file. It also loads up external stl files.
Currently the sidecutters are the only ones that are active.
#+begin_src clojure :tangle core.clj
(defn -main
  [& _]
  (binding [scad-clj.model/*center* false]
    (mio/write-scad-with-import-cutters!
     "out/magwell.scad"
     (model)
     [{:pos (:pos p/sidecutter)   :rot (:rot p/sidecutter)   :stl "sidecutter.stl"}
      {:pos (:pos p/sidecutter-2) :rot (:rot p/sidecutter-2) :stl "sidecutter.stl"}])))
#+end_src
